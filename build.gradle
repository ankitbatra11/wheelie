apply plugin: "com.github.ben-manes.versions"

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.36.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    if (it.name != 'app') {
        apply plugin: 'com.android.library'
        apply plugin: 'com.github.dcendents.android-maven'
    } else {
        apply plugin: 'com.android.application'
    }
    group = 'com.github.ankitbatra11'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.2"

        buildFeatures {
            viewBinding = true
        }
        defaultConfig {
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            consumerProguardFiles "consumer-rules.pro"
            // Required for Java 8+ API(s) when setting minSdkVersion to 20 or lower
            multiDexEnabled true
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            // Flag to enable support for the new language APIs
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }
    }
    dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

        api 'com.jakewharton.timber:timber:4.7.1'
        api group: 'com.google.guava', name: 'guava', version: '29.0-android'

        testImplementation group: 'org.robolectric', name: 'robolectric', version: '4.5.1'
        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
        testImplementation group: 'androidx.test', name: 'core', version: '1.3.0'

        def mockitoVersion = '3.7.7'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testImplementation group: 'org.mockito', name: 'mockito-inline', version: mockitoVersion

        testImplementation 'org.awaitility:awaitility:4.1.0'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}